// VerilogA for dll_design, dff_reset, veriloga

`include "constants.vams"
`include "disciplines.vams"

// $Date: 1998/09/23 03:13:33 $
// $Revision: 1.2 $
//
//
// Based on the OVI Verilog-A Language Reference Manual, version 1.0 1996
//
//
 


//--------------------
// d_ff
//
// -  D-type flip flop
//
// vin_d:		[V,A]
// vclk:		[V,A]
// vout_q,vout_qbar:	[V,A]
//
// INSTANCE parameters
//    vlogic_high = output voltage for high [V]
//    vlogic_low  = output voltage for high [V]
//    vtrans      = voltages above this at input are considered high [V]
//    vtrans_clk  = transition voltage of clock [V]
//    tdel, trise, tfall = {usual} [s]
//
// Triggered on the rising edge
//
(* instrument_module *)
module dff_reset(vin_d, vclk, vout_q, vout_qbar, reset);
input vclk, vin_d, reset;
output vout_q, vout_qbar;
electrical vout_q, vout_qbar, vclk, vin_d, reset;
parameter real vlogic_high = 5;
parameter real vlogic_low = 0;
parameter real vtrans_clk = 2.5;
parameter real vtrans = 2.5;
parameter real reset_trans = 2.5;
parameter real tdel = 3u from [0:inf);
parameter real trise = 1u from (0:inf);
parameter real tfall = 1u from (0:inf);

   integer x;

   analog begin
      @ (cross( V(vclk) - vtrans_clk, +1 ) or cross(V(reset) - reset_trans, +1))
		if(V(reset) > reset_trans)
			x = 0;
		else if (V(vin_d) > vtrans)
			x = 1;
		else
			x = 0;
         V(vout_q) <+ transition( vlogic_high*x + vlogic_low*!x,
				   tdel, trise, tfall );
         V(vout_qbar) <+ transition( vlogic_high*!x + vlogic_low*x,
				      tdel, trise, tfall );
    end
endmodule






